:<:{ncds}
    <= $. %>({ncds})
    <- {derivation complete}
        <= ::(write completion status to progress file)
        <- {progress file path}<:{1}>
        <- {ncds output written}<:{2}>
    <- {ncds output written}
        <= ::(write ncds file to output path)
            <= @:!(<phase 5 already complete>)
            <* <phase 5 already complete>
        <- {output file path}<:{1}>
        <- {ncds content}<:{2}>
            <= ::(serialize dependency tree to ncds text format)
            <- {dependency tree}
        <- <phase 5 already complete>
            <= ::<{check if phase 5 already complete in progress}><ALL True>
            <- {current progress}
        <- {phase 4 complete}
    <- {phase 4 complete}
        <= ::(write phase 4 status to progress file)
        <- {progress file path}<:{1}>
        <- {tree file written}<:{2}>
    <- {tree file written}
        <= ::(write dependency tree to checkpoint file)
            <= @:!(<phase 4 already complete>)
            <* <phase 4 already complete>
        <- {tree file path}<:{1}>
        <- {dependency tree}<:{2}>
            <= ::(construct tree from classifications and extractions)
            <- {final output concept}<:{1}>
                <= ::(identify goal from extraction data)
                <- {extraction data}
            <- {classified operations}<:{2}>
                <= ::(read classifications from checkpoint)
                <- {classifications file path}
            <- [all dependencies]<:{3}>
                <= ::(extract dependencies from extraction data)
                <- {extraction data}
        <- <phase 4 already complete>
            <= ::<{check if phase 4 already complete in progress}><ALL True>
            <- {current progress}
        <- {phase 3 complete}
    <- {phase 3 complete}
        <= ::(write phase 3 status to progress file)
        <- {progress file path}<:{1}>
        <- {classifications file written}<:{2}>
    <- {classifications file written}
        <= ::(write classifications to checkpoint file)
            <= @:!(<phase 3 already complete>)
            <* <phase 3 already complete>
        <- {classifications file path}<:{1}>
        <- [all classifications]<:{2}>
            <= *. %>([extracted operations]) %<({classification}) %:({operation}) %@(1)
                <= $. %>({classification})
                <- {classification}
                    <= ::(determine pattern type for operation)
                    <- {current operation}<:{1}>
                    <- {operation context}<:{2}>
                        <= ::(gather context from extraction data)
                        <- {current operation}<:{1}>
                        <- {extraction data}<:{2}>
            <- [extracted operations]
                <= ::(get operations from extraction data)
                <- {extraction data}
            <* {current operation}<$([extracted operations])*>
        <- <phase 3 already complete>
            <= ::<{check if phase 3 already complete in progress}><ALL True>
            <- {current progress}
        <- {phase 2 complete}
    <- {phase 2 complete}
        <= ::(write phase 2 status to progress file)
        <- {progress file path}<:{1}>
        <- {extraction file written}<:{2}>
    <- {extraction file written}
        <= ::(write extraction results to checkpoint file)
            <= @:!(<phase 2 already complete>)
            <* <phase 2 already complete>
        <- {extraction file path}<:{1}>
        <- {extraction data}<:{2}>
            <= &[{}] %>[{extracted concepts}, [extracted operations], [extracted dependencies], [extracted control patterns]] %+(extraction)
            <- {extracted concepts}
                <= ::(identify all nouns and data entities)
                <- {refined instruction content}
            <- [extracted operations]
                <= ::(identify all verbs and actions)
                <- {refined instruction content}
            <- [extracted dependencies]
                <= ::(identify what needs what)
                <- {refined instruction content}<:{1}>
                <- {extracted concepts}<:{2}>
                <- [extracted operations]<:{3}>
            <- [extracted control patterns]
                <= ::(identify loops and conditions)
                <- {refined instruction content}
        <- <phase 2 already complete>
            <= ::<{check if phase 2 already complete in progress}><ALL True>
            <- {current progress}
        <- {phase 1 complete}
    <- {phase 1 complete}
        <= ::(write phase 1 status to progress file)
        <- {progress file path}<:{1}>
        <- {refined instruction file written}<:{2}>
    <- {refined instruction file written}
        <= ::(write refined instruction to checkpoint file)
            <= @:!(<phase 1 already complete>)
            <* <phase 1 already complete>
        <- {refined instruction file path}<:{1}>
        <- {refined instruction content}<:{2}>
            <= $. %>[{cached refined instruction}, {freshly refined instruction}]
            <- {cached refined instruction}
                <= ::(read refined instruction from checkpoint)
                    <= @:'(<phase 1 already complete>)
                    <* <phase 1 already complete>
                <- {refined instruction file path}
            <- {freshly refined instruction}
                <= $. %>[{refined version}, {original version}]
                    <= @:!(<phase 1 already complete>)
                    <* <phase 1 already complete>
                <- {refined version}
                    <= ::(synthesize from refinement answers)
                    <= @:'(<instruction is vague>)
                    <* <instruction is vague>
                <- {refinement answers}
                    <= ::(apply each refinement question)
                    <- {raw instruction content}<:{1}>
                    <- {refinement questions content}<:{2}>
                        <= ::(read refinement questions from file)
                        <- {refinement questions file path}
            <- {original version}
                <= ::(pass through raw instruction)
                    <= @:!(<instruction is vague>)
                    <* <instruction is vague>
                <- {raw instruction content}
            <- <instruction is vague>
                <= ::<{judge if instruction lacks concrete details}><ALL True>
                <- {raw instruction content}
        <- <phase 1 already complete>
            <= ::<{check if phase 1 already complete in progress}><ALL True>
            <- {current progress}
    <- {raw instruction content}
        <= ::(read instruction from input file)
        <- {instruction file path}
    <- {current progress}
        <= ::(read progress from file if exists)
        <- {progress file path}
    <- {instruction file path}
    <- {refinement questions file path}
    <- {progress file path}
    <- {refined instruction file path}
    <- {extraction file path}
    <- {classifications file path}
    <- {tree file path}
    <- {output file path}
