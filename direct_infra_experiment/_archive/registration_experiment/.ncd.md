To register a new user, first check if the provided username already exists in the database. If it does, report an error. Otherwise, create a new user account.

:<:(::(register a new user))
    ?: How to register a new user?
    <= @by(:_:)
        /: The registration is requesting another process to be executed.
    <- :_:{steps}({user name})
        /: The process is normatively bounded in steps with a given user name.
    <- {user name}
        ...: The provided username is provided by the user.
        ?: How is the username inputted?
        <= :>:({prompt}<$({user name}?)%>)
            /: The {user name} is inputted by the user with the help of description in the {user name}? prompt.
        <- {user name}?<:{prompt}>
            /: The description in the {user name}? prompt.
            |%{prompt_location}: 0_user_name_prompt

    <- {steps}
        ...: First, check if the provided username already exists in the database.
        ...: Second, if it does, report an error.
        ...: Third, otherwise, create a new user account.
        ?: How are the steps composed?

        <= &across
            /: The steps are an ordered sequence of steps of actions without an overall outcome.

        <- {step 1}
            ...: First, check if the provided username already exists in the database.
            ?: What is step 1?
            <= $::.<username exists>
                /: Step 1 is nominalized as judging the truth of the statement <username exists>.
            <- ::{%(python)}<{script}<$({username exists in the database})%>: {1}<$({user name})%>, {2}<$({database})%>>
                /: The subject will judge in python mode according to the script of {username exists in the database} with the provided {user name}.
            <- {username exists in the database}<:{script}>
                |%{script_location}: 1_username_exists_in_database_script
                |%{prompt_location}: 1_username_exists_in_database_prompt
            <- {user name}<:{1}>
                /: {user name} as a variable is given to the nominalized action.
            <- {database}<:{2}>
                /: The database is the database of the system specified in a memorized parameter.
                |%{memorized_parameter}: user_database

        <- {step 2}
            ...: Second, if it does, report an error.
            ?: What is step 2?
            <= $::.{error}
                /: Step 2 is nominalized as the action of reporting an error.
            <- ::{%(python)}<{script}<$({report error})%>>
                /: The action is to report an error, if the username already exists.
                ?: When should an error be reported?
                <= @if(<username exists>)
                    /: An error is reported if the username already exists.
                <- <username exists>
                    /: <username exists> as a variable is given to the nominalized action.
            <- {report error}<:{script}>
                |%{script_location}: 2_report_error_script
                |%{prompt_location}: 2_report_error_prompt
            
        <- {step 3}
            ...: Third, otherwise, create a new user account.
            ?: What is step 3?
            <= $::.{new user account}
                /: Step 3 is nominalized as the action of creating a new user account.
            <- ::{%(python)}<{script}<$({create new user account})%>: {1}<$({user name})%>, {2}<$({database})%>>
                /: The action is to create a new user account, if the username does not exist.
                ?: When should a new user account be created?
                <= @if!(<username exists>)
                    /: A new user account is created if the username does not exist.
                <- <username exists>
                    /: <username exists> as a variable is given to the nominalized action.
            <- {user name}<:{1}>
                /: {user name} as a variable is given to the nominalized action.
            <- {database}<:{2}>
                /: The database is the database of the system specified in a memorized parameter.
                |%{memorized_parameter}: user_database
            <- {create new user account}<:{script}>
                |%{script_location}: 3_create_new_user_account_script
                |%{prompt_location}: 3_create_new_user_account_prompt


    